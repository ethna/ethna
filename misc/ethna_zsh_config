#compdef ethna

# usage:
#   1. copy this file to your favorite directory (ex, ~/.zshrc.d/completion)
#   2. add below lines to your .zshrc:
#      > fpath=(~/.zshrc.d/completion $fpath)
#      > autoload -U compinit
#      > compinit -u
#   3. push [tab] when you typing ethna command's argument.
#      % ethna add-[tab]
#
# note:
#   - we tested this function only with Debian/GNU Linux.
#   - $Id$

_ethna() {
    local curcontext="$curcontext" state line expl usage ret=1

    _arguments -C \
        '(-v --version)'{-v,--version}'[show version]' \
        '1:ethna command:->ethna-cmd' \
        '*:ethna command args:->ethna-cmd-args' \
        && ret=0

    if [[ -n "$state" ]]; then
	[[ "$words[2]" = 'help' ]] && state=ethna-cmd
        case $state in
            ethna-cmd)
                handles=( $($service | awk '$0~"^ +[^ -]"{print $1}') )
                _wanted ethna-cmd expl 'ethna command' compadd -a handles && ret=0
            ;;
            ethna-cmd-args)
                usage=$($service help "$words[2]")
                if echo "$usage" | grep -q 'command not found.'; then
                    _wanted ethna-cmd-arg expl 'ethna command args' compadd -x "unknown command" && ret=0
                else
                    _wanted ethna-cmd-arg expl 'ethna command args' compadd -x "$usage" && ret=0
                fi
            ;;
        esac
    fi

    return ret
}
